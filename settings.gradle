pluginManagement {
    repositories {
        // Use private Reposilite only when explicitly set for internal network builds
        if (System.getenv('CI_ENV') == 'internal') {
            maven {
                url = uri('https://maven.rokkon.com/gradle-plugins/')
            }
        }
        // Default to public Gradle Plugin Portal (always available)
        gradlePluginPortal()
    }
}

rootProject.name = 'platform-registration-service'

dependencyResolutionManagement {
//    repositoriesMode.set(RepositoriesMode.PREFER_SETTINGS)

    versionCatalogs {
        libs {
            // Import version catalog from published BOM
            // Available on both private Reposilite and public GitHub Packages
            from("ai.pipestream:pipeline-bom-catalog:0.1.0-SNAPSHOT")
        }
    }

    repositories {
        // Maven Local for published Pipeline artifacts during development (releases only)
        mavenLocal {
            mavenContent {
                releasesOnly()
            }
            content {
                includeGroupByRegex "ai\\.pipestream(\\..*)?"
            }
        }

        // Maven Central (always available, default for public builds)
        mavenCentral()

        // Maven Central Snapshots for consuming published SNAPSHOTs
        maven {
            url = uri('https://central.sonatype.com/repository/maven-snapshots/')
            mavenContent {
                snapshotsOnly()
            }
        }

        // Apache snapshots for Tika 4.x pre-release (default for public builds)
        if (System.getenv('CI_ENV') != 'internal') {
            maven {
                url = uri('https://repository.apache.org/content/repositories/snapshots/')
                allowInsecureProtocol = false
                content {
                    includeGroupByRegex "org\\.apache\\.tika(\\..*)?"
                }
            }
        }
    }
}
